FUNCTION CENTRINO_LATO1(REAL $LATO1_DIAMETER) {

DEACTIVATE Workplane
		ACTIVATE FOLDER "Featureset\STANDARD_HOLES\ENTRATA"
		EDIT FEATURECREATE NAME   
		EDIT FEATURESET ; INSERT FILE FILEOPEN "V:\Master Files\Matrex\Powermill\Imports\Centering Holes\CENTERING HOLES.xml"
		DELETE Workplane "1"
		RENAME Featureset ; "CENTERING HOLES"
		DEACTIVATE FOLDER
		//Centering hole left
		EDIT FEATURESET "CENTERING HOLES" SELECT "CENTERING LEFT"
		EDIT FEATURESET ; FEATURE SELECTED POSITION  X "-$LATO1_DIAMETER/2 + 10"
		EDIT FEATURESET "CENTERING HOLES" DESELECT "CENTERING LEFT"
		//Centering hole top
		EDIT FEATURESET "CENTERING HOLES" SELECT "CENTERING TOP"
		EDIT FEATURESET ; FEATURE SELECTED POSITION  Y "$LATO1_DIAMETER/2 - 10"
		EDIT FEATURESET "CENTERING HOLES" DESELECT "CENTERING TOP"
		//Centering hole right
		EDIT FEATURESET "CENTERING HOLES" SELECT "CENTERING RIGHT"
		EDIT FEATURESET ; FEATURE SELECTED POSITION  X "$LATO1_DIAMETER/2 - 10"
		EDIT FEATURESET "CENTERING HOLES" DESELECT "CENTERING RIGHT"
	
		//Centering Holes             Toolpath            Index        Tool    Depth
		//                              10        20        30        40        50
		//                      012345678901234567890123456789012345678901234567890
		STRING $PACKET       = "CENTERING HOLES               1         T203      8"
		STRING $PATHNAME = RTRIM(SUBSTRING($PACKET , 0 , 29))
		INT $INDEX = SUBSTRING($PACKET , 30 , 1)
		STRING $TOOLNUMBER = SUBSTRING($PACKET , 40 , 4)
		REAL $DRILLDEPTH = SUBSTRING($PACKET , 50 , 3)
		STRING $NAME = ""
		REAL $SPEED = 0
		REAL $FEED = 0
		REAL $PLUNGE = 0
		EDIT TOOLPATH THICKNESS LIST UPDATE 0 NEW	
		
		IF ENTITY_EXISTS('Toolpath' , $PATHNAME) {
			DELETE Toolpath $PATHNAME NOQUIBBLE
		}
		//Import all toolpaths
		IF $MANDREL OR $ALLARGATORE {
			IMPORT TEMPLATE PROJECT FILEOPEN "V:\Master Files\Matrex\Powermill\Tamplates\Entrata\Centrino.ptf"
			//Select Solid to ignore it
			BLANK UNDO
			EDIT LEVEL "264" SELECT ALL
		} ELSE {
			IMPORT TEMPLATE PROJECT FILEOPEN "V:\Master Files\Matrex\Powermill\Tamplates\Entrata\Centrino.ptf"
			//Select Solid to ignore it
			BLANK UNDO
			EDIT LEVEL "20" SELECT ALL
		}
		
		
		//Drill Toolpath
		ACTIVATE Toolpath $PATHNAME
		//Depth
		EDIT DRILL DEPTH $DRILLDEPTH
		//Tool
		IF $COMPANY == "Matrex" {
			CALL $MatrexToolsForDrilling($TOOLNUMBER , $NAME , $SPEED , $FEED , $PLUNGE)
		} ELSEIF $COMPANY == "Alumat" {
			CALL $AlumatToolsForDrilling($TOOLNUMBER , $NAME , $SPEED , $FEED , $PLUNGE)
		} ELSE {
			MESSAGE ERROR "Uknown Company"
		}
		ACTIVATE TOOL $NAME
		EDIT RPM $SPEED
		EDIT FRATE $FEED
		EDIT PRATE $PLUNGE
		//Rapids
		EDIT TOOLPATH SAFEAREA SIZE "2"
		EDIT TOOLPATH SAFEAREA PLUNGE_SIZE  "2"
		//Component Thickness
		EDIT TOOLPATH THICKNESS LIST UPDATE 0 NEW
		EDIT TOOLPATH ; THICKNESS ACQUIRE
		EDIT TOOLPATH ; THICKNESS COMPONENTS IGNORE
		//Select Featureset Components
		ACTIVATE Featureset "CENTERING HOLES"
		EDIT FEATURESET "CENTERING HOLES" SELECT "CENTERING LEFT"
		EDIT FEATURESET "CENTERING HOLES" SELECT "CENTERING TOP"
		EDIT FEATURESET "CENTERING HOLES" SELECT "CENTERING RIGHT"
		//Calculate
		EDIT TOOLPATH $PATHNAME CALCULATE
		BLANK UNDO
		DEACTIVATE Toolpath
		DEACTIVATE Tool
		DEACTIVATE Featureset

}